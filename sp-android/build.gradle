apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.1.4'
    }
}

repositories {
    mavenCentral()
    maven {
        url 'https://maven.google.com/'
        name 'Google'
    }
}

android {
    compileSdkVersion 34
    namespace = "net.webilisim.spunit"
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 34
    }

    compileOptions {
		sourceCompatibility JavaVersion.VERSION_17
		targetCompatibility JavaVersion.VERSION_17
	}
}

class SDPFactory extends DefaultTask {

    @Input
    String unit = "sp"
    @Input
    double positiveMax = 600
    @Input
    double negativeMax = 60

    @TaskAction
    def create() {
        String resFolder =  project.getProjectDir().getPath() + "/src/main/res/"
        for(double dimen = 300; dimen <= 1080 ; dimen = dimen + 30){
            String folder = resFolder + "values-sw" + (int)dimen + "dp"
            String fileName = folder + "/positive_sps.xml"
            new File(folder).mkdir()
            new File(fileName).createNewFile()
            PrintWriter printWriter = new PrintWriter(fileName)
            printWriter.println("<?xml version=\"1.0\" encoding=\"utf-8\"?>")
            printWriter.println("<resources>")
            for(int i=1; i<=positiveMax; i++){
                double ratio = i/300d
                double sdp = ratio*dimen
                printWriter.printf("\t<dimen name=\"_%dsp\">%.2f" + unit + "</dimen>\r\n",i,sdp)
            }
            printWriter.println("</resources>")
            printWriter.close()
        }
        for(double dimen = 300; dimen <= 1080; dimen = dimen + 30){
            String folder = resFolder + "values-sw" + (int)dimen + "dp"
            String fileName = folder + "/negative_sps.xml"
            new File(folder).mkdir()
            new File(fileName).createNewFile()
            PrintWriter printWriter = new PrintWriter(fileName)
            printWriter.println("<?xml version=\"1.0\" encoding=\"utf-8\"?>")
            printWriter.println("<resources>")
            for(int i=1;i<=negativeMax;i++){
                double ratio = i/300d
                double sdp = ratio*dimen
                printWriter.printf("\t<dimen name=\"_minus%dsp\">%.2f" + unit + "</dimen>\r\n",i,-sdp)
            }
            printWriter.println("</resources>")
            printWriter.close()
        }
    }
}

tasks.register('createSDP', SDPFactory) {
}

//uncomment next line to edit values
//preBuild.dependsOn createSDP

createSDP{
    unit = "sp"
    positiveMax = 600//change to 600 or any other value if needed
    negativeMax = 60//change to 600 or any other value if needed
}

publishing {
    publications {
        release(MavenPublication) {

            artifactId = 'sp-android'
            groupId = 'net.webilisim.spunit'
            version = '1.0.1'
            artifact("$buildDir/outputs/aar/sp-android-release.aar")

            pom {
                name = 'sp'
                description = 'A scalable size unit for Andorid'
                url = 'https://github.com/webilisim/sp-unit'
                licenses {
                    license {
                        name = 'The MIT License (MIT)'
                        url = 'https://github.com/intuit/sdp/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = "net.webilisim"
                        name = "We Bilisim Interactive"
                        email = "info@webilisim.net"
                    }
                }
                scm {
                    connection = 'scm:git@github.com:webilisim/sp-unit.git'
                    developerConnection = 'scm:git@github.com:webilisim/sp-unit.git'
                    url = 'https://github.com/webilisim/sp-unit'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.release
}
